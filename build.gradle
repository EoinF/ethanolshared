group 'com.github.eoinf.ethanolshared'
version '1.6-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

sourceCompatibility = 1.8

sourceSets.main.java.srcDirs = [ "src/" ]
project.ext.assetsDir = 'assets'

ext {
    gdxVersion = '1.9.6'
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "net.dermetfan.libgdx-utils:libgdx-utils:0.10.0"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
}

task('buildAssets', type: JavaExec, dependsOn: 'classes') {
    main = 'com.github.eoinf.ethanolshared.GenerateAssets'
    classpath = sourceSets.main.runtimeClasspath
}

task('dist', type: Jar, dependsOn: 'buildAssets') {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from files(project.assetsDir)

    // Skip the creation of the default jar artifact
    jar.enabled = false
}

task('archiveDist', dependsOn: 'dist') {
    doLast {
        project.configurations.archives.artifacts.clear()
        project.artifacts {
            archives dist
        }
    }
}

task('releaseSnapshot', type: Upload, dependsOn: 'archiveDist') {
    repositories {
        configuration = project.configurations.archives
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'EthanolShared'
                packaging 'jar'
                description 'Shared assets for the cup of methanol game and its editor'
                url 'https://github.com/EoinF/ethanolshared'

                scm {
                    url 'scm:git@github.com:EoinF/ethanolshared.git'
                    connection 'scm:git@github.com:EoinF/ethanolshared.git'
                    developerConnection 'scm:git@github.com:EoinF/ethanolshared.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'outterest'
                        name 'Eoin Flanagan'
                    }
                }
            }
        }
    }
}